#!ipxe
# by dialmak
imgfree
clear target
isset ${amazon} && goto start-menu ||

# Разкомментируйте строчки ниже и впишите свой SSID и KEY для WLAN с WPA\WPA2, при необходимости замените net0 на net1.
set net0/ssid home93
set net0/key 89062483473

# Установка переменных
set cls:hex 1b:5b:4a
set cls ${cls:string}
set space:hex 20:20
set space ${space:string}
set sp:hex 20
set sp ${sp:string}
set amazon http://d2vilbl8ffdigq.cloudfront.net
set selectel http://137615.selcdn.ru/ipxe
cpuid --ext 29 && set arch x86_64 || set arch i386

# Получение настроек сети по DHCP
ifconf -c dhcp && goto start-menu ||
echo
echo DHCP failed. 
prompt Press any key to set ssid\key for WLAN or ip\netmask\dns for LAN\WAN manually.
:config
config

:start-menu
menu Welcome to iPXE's Boot Menu
item --gap Select boot server:
	item amazon ${space} dialmak's Boot Menu [ server amazon.com ]
	item selectel ${space} dialmak's Boot Menu [ server selectel.ru ]
	item boot_rackspac_com    ${space} boot.rackspace.com Boot Menu
	item netboot_xyz   ${space} netboot.xyz Boot Menu
	item salstar_sk    ${space} boot.salstar.sk Boot Menu
item --gap Tools:
	item view   ${space} Network setting
	item config ${space} Configure iPXE
	item shell  ${space} iPXE shell
	item reboot ${space} Reboot
	item exit   ${space} Exit
isset ${target} && set timeout 0 || set timeout 20000
choose --timeout ${timeout} --default ${target} target && goto ${target} || 
echo ${cls}
goto start-menu

:amazon
set server amazon.com
chain ${amazon}/menu.ipxe || goto failed
goto start-menu

:selectel
set server selectel.ru
chain ${selectel}/menu.ipxe || goto failed
goto start-menu

:netboot_xyz
chain ${amazon}/netboot.xyz.lkrn || goto failed
goto start-menu

:boot_rackspac_com
chain http://boot.rackspace.com/menu.ipxe || goto failed
goto start-menu

:salstar_sk
chain http://boot.salstar.sk/ipxe/ipxe.lkrn || goto failed
goto start-menu

:reboot
reboot --warm ||
goto start-menu

:view
isset ${memsize} && echo ${manufacturer} | ${product} | ${platform} | ${arch} | ${memsize} MiB || echo ${manufacturer} | ${product} | ${platform} | ${arch} 
echo -- ------------------------------------------------------------------------
isset ${proxydhcp/filename} && echo The network settings obtained from the a proxyDHCP server || set man-flag 1
#isset ${netX.dhcp/filename} && echo The network settings obtained from the a DHCP server || set man-flag 2
isset ${netX.dhcp/dhcp-server} && echo The network settings obtained from the a DHCP server || set man-flag 3
isset ${man-flag} || echo The network settings set manually
echo
echo ${ifname} is a  ${chip} ${pci/${busloc}.0.2}:${pci/${busloc}.2.2}
echo mac  	${sp}: ${mac}
echo ip  ${space}: ${ip}
echo mask		 : ${netmask}
echo gw  ${space}: ${gateway}
echo dns    ${sp}: ${dns}  
echo tftp		 : ${next-server} 
echo dhcp		 : ${dhcp-server}
echo http		 : ${boot-url}
echo file		 : ${filename}
echo
prompt Press any key to back to the menu ...
goto start-menu

:shell
echo ${cls}
echo Type "help" to get the list commands.
echo Type "exit" to get the back to the menu.
echo
shell
goto start-menu

:failed
imgfree
echo
prompt Booting failed, press any key to back to the menu ...
goto start-menu

:exit
echo Continuing BIOS boot order...
sleep 1
exit 0

