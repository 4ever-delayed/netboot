#!ipxe

goto ${menu} ||

:live_menu
set os Feren OS Live
menu ${os} - Current Arch [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
item --gap ${os} Versions
item current ${space} ${os} Current
choose live_version || goto live_exit
menu ${os} ${live_version}
item --gap ${os} Flavors
goto ${live_version}

:current
item /ubuntu-squash/releases/download/12-2019-9536481c/ ${space} Feren Standard
item /ubuntu-squash/releases/download/12-2019-d30d6c1b/ ${space} Feren Classic
choose path || goto live_menu
goto feren-boot

:feren-boot
imgfree
set squash_url ${live_endpoint}${path}filesystem.squashfs
set kernel_url ${live_endpoint}${path}
kernel ${kernel_url}vmlinuz ip=dhcp boot=casper netboot=http fetch=${squash_url} initrd=initrd ${cmdline}
initrd ${kernel_url}initrd
boot

:live_exit
clear menu
exit 0

