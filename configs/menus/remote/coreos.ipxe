#!ipxe

# Fedora CoreOS
# https://getfedora.org/coreos/
# https://github.com/coreos/coreos-installer/blob/master/README.md#installing-from-pxe

goto ${menu}

:coreos
set os Fedora CoreOS
isset ${install_device} || set install_device /dev/sda
isset ${ignition_url} || set ignition_url skip
menu ${os}
item --gap ${os}:
item 33.20210328.3.0 ${space} ${os} stable
item install_dev ${space} Set install device: ${install_device}
item ignition_config ${space} Set ignition config url: ${ignition_url}
choose --default ${core_version} core_version || goto coreos_exit
echo ${cls}
iseq ${core_version} ignition_config && goto ignition_config ||
iseq ${core_version} install_dev && goto install_dev ||
goto core_boot ||
goto coreos_exit

:core_boot
iseq ${core_version} 33.20210328.3.0 && set coreos_channel stable ||
set base_url ${coreos_mirror}/${coreos_base_dir}/${coreos_channel}/builds
set build_version ${core_version}
imgfree
kernel ${base_url}/${build_version}/x86_64/fedora-coreos-${build_version}-live-kernel-x86_64 ip=dhcp rd.neednet=1 coreos.inst.install_dev=${install_device} coreos.inst.ignition_url=${ignition_url} coreos.live.rootfs_url=${base_url}/${build_version}/x86_64/fedora-coreos-${build_version}-live-rootfs.x86_64.img initrd=fedora-coreos-${build_version}-live-initramfs.x86_64.img ${cmdline}
initrd ${base_url}/${build_version}/x86_64/fedora-coreos-${build_version}-live-initramfs.x86_64.img
boot
goto coreos_exit

:install_dev
echo -n Please set desired install device:  && read install_device
clear menu
goto coreos

:ignition_config
echo -n Please set Ignition Configuration URL:  && read ignition_url
clear menu
goto coreos

:coreos_exit
clear menu
exit 0
