#!ipxe

goto ${menu} ||

:live_menu
set os Clonezilla
menu ${os} - Current Arch [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
item --gap ${os} Versions
item debian ${space} ${os} Debian Based
item ubuntu ${space} ${os} Ubuntu Based
choose live_version || goto live_exit
menu ${os} ${live_version}
item --gap ${os} Flavors
goto ${live_version}

:debian
item /debian-squash/releases/download/2.7.2-38-fcb50247/ ${space} Clonezilla Debian Stable
item /debian-squash/releases/download/2.7.2-39-002d59cc/ ${space} Clonezilla Debian Testing
choose path || goto live_menu
goto clonezilla-boot

:ubuntu
item /ubuntu-squash/releases/download/20210530-hirsute-2a768141/ ${space} Clonezilla Ubuntu Stable
item /ubuntu-squash/releases/download/20210609-hirsute-18e78f5f/ ${space} Clonezilla Ubuntu Testing
choose path || goto live_menu
goto clonezilla-boot

:clonezilla-boot
imgfree
set url ${live_endpoint}${path}
kernel ${url}vmlinuz boot=live username=user union=overlay config components noswap edd=on nomodeset ocs_live_run="ocs-live-general" ocs_live_batch=no net.ifnames=0 nosplash noprompt fetch=${url}filesystem.squashfs initrd=initrd ${cmdline}
initrd ${url}initrd
boot

:live_exit
clear menu
exit 0

